apply plugin: 'java'
//apply from: "https://raw.githubusercontent.com/mental-soft/gradle-config/master/global-config-jar.gradle"

//pmd {
//    ignoreFailures = true
//}

repositories {
    jcenter()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'

    testCompile 'junit:junit:4.12'
}


buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

bintray {
    user = project.hasProperty('bintray-user') ? project.property('bintray-user') : System.getenv('bintray-user')
    key = project.hasProperty('bintray-key') ? project.property('bintray-key') : System.getenv('bintray-key')

    publications = ['BintrayPublication']
    pkg {
        repo = 'meparty'
        name = 'com.teammental.memapper'
        desc = 'Mental Soft Generic Mapper Package'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mental-party/memapper.git'
        userOrg = 'mental-party'
        labels = ['builder', 'java', 'generic']
        publicDownloadNumbers = true
        version {
            name = System.getProperty('bambooPublishNumber')
            desc = 'Mental Generic Mapper'
        }
    }
}


// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "coskundeniz89"
            name "Co≈ükun Deniz"
            email "coskundeniz1989@gmail.com"
        }
    }

    scm {
        url "https://github.com/mental-party/memapper"
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.teammental'
            artifactId 'memapper'
            version System.getProperty('bambooPublishNumber')
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Mental Generic Mapper')
                root.appendNode('name', 'Mental Mapper')
                root.appendNode('url', 'https://github.com/mental-party/memapper')
                root.children().last() + pomConfig
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}